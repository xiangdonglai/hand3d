#version 120

// Input vertex data, different for all executions of this shader.
attribute vec3 vertexPosition_modelspace;
attribute vec3 vertexNormal_modelspace;
attribute vec2 vertexUV;


// Values that stay constant for the whole mesh.
uniform mat4 MVP;
uniform mat4 M;
uniform mat4 V;
uniform mat4 iMV;
uniform mat4 MVPp;
uniform vec3 LightPosition1_worldspace;
uniform vec3 LightPosition2_worldspace;
uniform vec3 ProjectorPosition_worldspace;

// Output data ; will be interpolated for each fragment.
varying vec2 UV;
varying vec3 Position_worldspace;
varying vec3 Normal_cameraspace;
varying vec3 EyeDirection_cameraspace;
varying vec3 LightDirection1_cameraspace;
varying vec3 LightDirection2_cameraspace;
varying vec3 ProjectorDirection_cameraspace;
varying vec4 vPro;

void main(){

	// Output position of the vertex, in camera space : MVP * position
	gl_Position =  MVP * vec4(vertexPosition_modelspace,1);
	
	// Position of the vertex, in worldspace : M * position
	Position_worldspace = (M * vec4(vertexPosition_modelspace,1)).xyz;
	
	// Vector that goes from the vertex to the camera, in camera space. In camera space, the camera is at the origin (0,0,0).
	vec3 vertexPosition_cameraspace = ( V * M * vec4(vertexPosition_modelspace,1)).xyz;
	EyeDirection_cameraspace = vec3(0,0,0) - vertexPosition_cameraspace;

		// Normal of the the vertex, in camera space
	Normal_cameraspace = ( iMV* vec4(vertexNormal_modelspace,0)).xyz;

	// Vector that goes from the vertex to the light, in camera space. M is ommited because it's identity.
	vec3 LightPosition1_cameraspace = ( V * vec4(LightPosition1_worldspace,1)).xyz;
	LightDirection1_cameraspace = LightPosition1_cameraspace + EyeDirection_cameraspace;

	// Vector that goes from the vertex to the light, in camera space. M is ommited because it's identity.
	vec3 LightPosition2_cameraspace = ( V * vec4(LightPosition2_worldspace,1)).xyz;
	LightDirection2_cameraspace = LightPosition2_cameraspace + EyeDirection_cameraspace;

	// Vector that goes from the vertex to the projector, in camera space. M is ommited because it's identity.
	vec3 ProjectorPosition_cameraspace = ( V * vec4(ProjectorPosition_worldspace,1)).xyz;
	ProjectorDirection_cameraspace = ProjectorPosition_cameraspace + EyeDirection_cameraspace;
	
	vPro =  MVPp * vec4(vertexPosition_modelspace,1);

	// UV of the vertex. No special space for this one.
	UV = vertexUV;
}

